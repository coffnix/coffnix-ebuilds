# Distributed under the terms of the GNU General Public License v2

EAPI=7

inherit autotools desktop toolchain-funcs xdg-utils

DESCRIPTION="A fast and lightweight web browser running in both graphics and text mode"
HOMEPAGE="http://links.twibright.com/"
SRC_URI="{% for artifact in artifacts %}{{ artifact.src_uri }}
	{% endfor %}
"

LICENSE="GPL-2"
SLOT="2"
KEYWORDS="*"
IUSE="brotli bzip2 fbcon freetype gpm ipv6 jpeg libevent livecd lzip lzma ssl suid svga tiff unicode webp X zlib zstd"

GRAPHICS_DEPEND="media-libs/libpng:="

RDEPEND="
	dev-libs/libbsd
	fbcon? ( ${GRAPHICS_DEPEND} )
	gpm? ( sys-libs/gpm )
	jpeg? ( media-libs/libjpeg-turbo:= )
	libevent? ( dev-libs/libevent:= )
	ssl? ( dev-libs/openssl:= )
	tiff? ( media-libs/tiff:= )
	webp? ( media-libs/libwebp:= )
	freetype? (
		media-libs/fontconfig
		media-libs/freetype
	)
	livecd? (
		${GRAPHICS_DEPEND}
		sys-libs/gpm
		media-libs/libjpeg-turbo:=
	)
	svga? (
		${GRAPHICS_DEPEND}
		media-libs/svgalib
	)
	X? (
		${GRAPHICS_DEPEND}
		x11-libs/libXext
	)
	brotli? ( app-arch/brotli:= )
	bzip2? ( app-arch/bzip2 )
	lzip? ( app-arch/lzip )
	lzma? ( app-arch/xz-utils )
	zlib? ( sys-libs/zlib )
	zstd? ( app-arch/zstd:= )
"

DEPEND="${RDEPEND}
	fbcon? ( virtual/os-headers )
	livecd? ( virtual/os-headers )"

BDEPEND="virtual/pkgconfig"

IDEPEND="X? ( dev-util/desktop-file-utils )"

REQUIRED_USE="!livecd? ( fbcon? ( gpm ) )
	svga? ( suid )"

DOCS=( AUTHORS BRAILLE_HOWTO ChangeLog KEYS NEWS README SITES )

src_prepare() {
	use X && xdg_environment_reset

	pushd intl > /dev/null || die
	./gen-intl || die
	./synclang || die
	popd > /dev/null || die

	# error: conditional "am__fastdepCXX" was never defined (for eautoreconf)
	sed -i \
		-e '/AC_PROG_CXX/s:dnl ::' \
		-e 's:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:' \
		configure.in || die #467020

	# Upstream configure produced by broken autoconf-2.13. This also fixes
	# toolchain detection.
	mv configure.in configure.ac || die

	default
	eautoreconf #131440 and #103483#c23
}

src_configure() {
	local myconf

	if use livecd; then
		export ac_cv_lib_gpm_Gpm_Open=yes
	else
		export ac_cv_lib_gpm_Gpm_Open=$(usex gpm)
	fi

	if use X || use fbcon || use svga || use livecd; then
		myconf+=' --enable-graphics'
	fi

	tc-export PKG_CONFIG

	local myeconfargs=(
		--without-directfb
		--without-librsvg
		$(use_with livecd fb)
		$(use_with livecd libjpeg)
		$(use_with brotli)
		$(use_with bzip2)
		$(use_with fbcon fb)
		$(use_with freetype)
		$(use_with ipv6)
		$(use_with jpeg libjpeg)
		$(use_with libevent)
		$(use_with lzip)
		$(use_with lzma)
		$(use_with ssl)
		$(use_with svga svgalib)
		$(use_with tiff libtiff)
		$(use_with webp libwebp)
		$(use_with X x)
		$(use_with zlib)
		$(use_with zstd)
		$(use_enable unicode utf8)
	)

	econf "${myeconfargs[@]}" ${myconf}
}

src_install() {
	HTML_DOCS="doc/links_cal/*"
	default

	if use X; then
		newicon "${DISTDIR}"/links-graphics-xlinks-logo-pic.png links.png
		make_desktop_entry 'links -g %u' Links links 'Network;WebBrowser'
		local d="${ED}"/usr/share/applications
		echo 'MimeType=x-scheme-handler/http;' >> "${d}"/*.desktop || die
		if use ssl; then
			sed -i -e 's:x-scheme-handler/http;:&x-scheme-handler/https;:' \
			"${d}"/*.desktop || die
		fi
	fi

	use suid && fperms 4755 /usr/bin/links
}

pkg_postinst() {
	use X && xdg_desktop_database_update
}

pkg_postrm() {
	use X && xdg_desktop_database_update
}
